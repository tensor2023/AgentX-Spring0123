{
  lib,
  inputs,
  namespace,
  system,
  pkgs,
  stdenv,
  fetchFromGitHub,
  nodejs,
  pnpm_10,
  ...
}:

let
  version = "1.25.2";
in

stdenv.mkDerivation (finalAttrs: {
  pname = "nanocoder";
  inherit version;

  src = fetchFromGitHub {
    owner = "nano-collective";
    repo = "nanocoder";
    rev = "v${version}";
    sha256 = "sha256-+flJPeLAlLWXw+yX2g7pp3rZFUN6YFFIntztP08cMTY=";
  };

  nativeBuildInputs = [
    nodejs
    pnpm_10.configHook
  ];

  pnpmDeps = pnpm_10.fetchDeps {
    inherit (finalAttrs) pname version src;
    hash = "sha256-GDMLRvEb1RJUcsIrwSTs9aBPYGVga4ZWj/gIX2LXjc0=";
    fetcherVersion = 2;
  };

  buildPhase = ''
    runHook preBuild
    pnpm run build
    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir -p $out/bin
    mkdir -p $out/lib/nanocoder

    # Copy built files
    cp -r dist $out/lib/nanocoder/
    cp -r node_modules $out/lib/nanocoder/
    cp package.json $out/lib/nanocoder/
    cp -r plugins $out/lib/nanocoder/

    # Copy static files not bundled by tsc (loaded at runtime via __dirname)
    install -D source/config/themes.json $out/lib/nanocoder/source/config/themes.json
    mkdir -p $out/lib/nanocoder/source/app/prompts
    cp -r source/app/prompts/* $out/lib/nanocoder/source/app/prompts/

    # Create wrapper script
    cat > $out/bin/nanocoder <<EOF
#!/usr/bin/env bash
NODE_PATH="$out/lib/nanocoder/node_modules" exec ${nodejs}/bin/node "$out/lib/nanocoder/dist/cli.js" "\$@"
EOF

    chmod +x $out/bin/nanocoder

    runHook postInstall
  '';

  meta = with lib; {
    description = "A beautiful local-first coding agent running in your terminal - built by the community for the community ⚒";
    homepage = "https://github.com/Nano-Collective/nanocoder";
    license = licenses.mit;
    maintainers = with maintainers; [ lalit64 ];
  };
})
